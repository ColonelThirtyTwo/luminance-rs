#!/bin/zsh
#
# This script is a great helper when having to perform roll out to crates.io. When the CI is all
# green and that you need to release a crate, you can use this tool to publish crates to crates.io.
#
# You will need crates.io credentials to call this script; use `cargo login <token>` to get your
# credentials.
#
# This script should be called from the root directory.

# List of crates to roll out. Order matters.
crates=(
  luminance-derive
  luminance
  luminance-std140
  luminance-gl
  luminance-glfw
  luminance-glutin
  luminance-sdl2
  luminance-webgl
  luminance-web-sys
  luminance-front
)

function auto_publish_crate() {
  echo "Publishing $1 to creates.ioâ€¦"

  ret=1
  try=1
  while [ $ret != 0 ]
  do
    echo "  try $try"
    (cd $1 && cargo publish)
    ret=$?

    if [ $ret != 0 ]
    then
      try=$(($try + 1))
      sleep 5
    else
      break
    fi
  done
}

function get_crate_version() {
  cargo read-manifest --manifest-path $1/Cargo.toml | jq -r ".version" | sed s/\.0$//
}

function auto_create_git_tag() {
  echo "Current tags for $1:"
  git tag | rg ^$1-\\d
  echo "Creating tag $1-$2?"
  (git tag $1-$2 && echo -e "  created git tag $1-\e[034;3m$2\e[0;0m")
}

function auto_push_git_tags() {
  echo "Pushing tags"
  git push origin --tags
}

function rollout() {
  for crate in $*; do
    version=$(get_crate_version $crate)
    echo -e "\e[032;1mRolling out \e[039;0m$crate-\e[034;3m$version\e[0;0m"

    auto_publish_crate $crate
    auto_create_git_tag $crate $version
  done

  auto_push_git_tags
}

case "$*" in
  "auto")
    rollout ${crates[@]}
    ;;

  *)
    rollout $*
    ;;
esac
